/** 
** Pong Game
** Paddle class
** Created by Stejara Dinulescu
**/


class Paddle {
	
	field int x, y; //x and y coordinates of paddle
	field int width; //width of paddle
	field int height; //height of paddle

	/* creates Paddle object */
	constructor Paddle new (int _x, int _y) {
		//sets attributes
		let x = _x; 
		let y = _y;
		let width = 10;
		let height = 20;
		//draws shape
		do draw();
		return this;
	}

	/* destroys allocated memory */
	method void dispose() {
      do Memory.deAlloc(this);
      return;
   }

   /* draws paddle on screen */
   method void draw() {
   	  do Screen.setColor(true); //displays shape
   	  do Screen.drawRectangle(x, y, x + width, y + height);
   	  return;
   }

   /* erases paddle from scren */ 
   method void erase() {
   	  do Screen.setColor(false);
   	  do Screen.drawRectangle(x, y, x + width, y + height);
   	  return;
   }

   /* moves paddle up by 1 pixel, method taken from Square.jack example */
   method void moveUp() {
	  if (y > 1) {
         do Screen.setColor(false);
         do Screen.drawRectangle(x, (y + height) - 1, x + width, y + height);
         let y = y - 1;
         do Screen.setColor(true);
         do Screen.drawRectangle(x, y, x + width, y + 1);
      }
      return;
   }

	/* moves paddle down by 1 pixel, method taken from Square.jack example */
   method void moveDown() {
   	  if ((y + height) < 254) {
         do Screen.setColor(false);
         do Screen.drawRectangle(x, y, x + width, y + 1);
         let y = y + 1;
         do Screen.setColor(true);
         do Screen.drawRectangle(x, (y + height) - 1, x + width, y + height);
      }
      return;
   }


}