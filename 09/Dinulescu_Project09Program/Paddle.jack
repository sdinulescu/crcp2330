/** 
** Pong Game
** Paddle class
** Created by Stejara Dinulescu
**/


class Paddle {
	
	field int x, y; //x and y coordinates of paddle
	field int width; //width of paddle
	field int height; //height of paddle

	/* creates Paddle object */
	constructor Paddle new (int _x, int _y) {
		//sets attributes
		let x = _x; 
		let y = _y;
		let width = 5;
		let height = 10;
		//draws shape
		do draw();
		return this;
	}

	/* destroys allocated memory */
	method void dispose() {
      do Memory.deAlloc(this);
      return;
   }

   /* draws paddle on screen */
   method void draw() {
   	  do Screen.setColor(true); //displays shape
   	  do Screen.drawRectangle(x, y, x + width, y + height);
   	  return;
   }

   /* erases paddle from scren */ 
   method void erase() {
   	  do Screen.setColor(false);
   	  do Screen.drawRectangle(x, y, x + width, y + height);
   	  return;
   }

   /* moves paddle up by 2 pixels, method taken from Square.jack example */
   method void moveUp() {
	  if (y > 1) {
         do Screen.setColor(false);
         do Screen.drawRectangle(x, (y + height) - 1, x + width, y + height);
         let y = y - 1;
         do Screen.setColor(true);
         do Screen.drawRectangle(x, y, x + width, y + 1);
      }
      return;
   }

	/* moves paddle down by 2 pixels, method taken from Square.jack example */
   method void moveDown() {
   	  if ((y + height) < 254) {
         do Screen.setColor(false);
         do Screen.drawRectangle(x, y, x + width, y + 1);
         let y = y + 1;
         do Screen.setColor(true);
         do Screen.drawRectangle(x, (y + height) - 1, x + width, y + height);
      }
      return;
   }

   /* moves paddle right by 2 pixels, method taken from Square.jack example */
   method void moveRight() {
      if ((x + width) < 510) {
         do Screen.setColor(false);
         do Screen.drawRectangle(x, y, x + 1, y + height);
         let x = x + 1;
         do Screen.setColor(true);
         do Screen.drawRectangle((x + width) - 1, y, x + width, y + height);
      }
      return;
   }

   /* moves paddle left by 2 pixels, method taken from Square.jack example */
   method void moveLeft() {
	  if (x > 1) {
         do Screen.setColor(false);
         do Screen.drawRectangle((x + width) - 1, y, x + width, y + height);
         let x = x - 1;
         do Screen.setColor(true);
         do Screen.drawRectangle(x, y, x + 1, y + height);
      }
      return;
   }

   /* describes movement of paddle with keypress */
   method void move() {
   	  var char key;
   	  let key = Keyboard.keyPressed();
   	  if ( ~(key = 0) ) {
   	  	if ( key = 97 ){
   	  		do moveLeft();
   	  	}
   	  	if ( key = 100 ) {
   	  		do moveRight();
   	  	}
   	  	if ( key = 119 ) {
   	  		do moveUp();
   	  	}
   	  	if ( key = 115 ) {
   	  		do moveDown();
   	  	}
   	  }
   	  return;
   }

}